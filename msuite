#!/usr/bin/perl
#
# Author : Kun Sun (sunkun@szbl.ac.cn)
# Date   : Aug 2020
# Version: 1.1.0
#
# This is the main program of Msuite.
# It will interpret the parameters, prepare data and write makefile.
#
# Msuite is freely available at https://github.com/hellosunking/Msuite/
#

use strict;
use warnings;
use Getopt::Long;
use Cwd;
use File::Basename;
use FindBin;
use lib "$FindBin::RealBin/bin";
use MsuiteVersion qw($version $ver);

## running environment
#my $abs_prg= `readlink -f $0`;
#chomp( $abs_prg );
#my $Msuite = dirname( $abs_prg );
#$Msuite    =~ s/\/\.$//;
my $Msuite = $FindBin::RealBin;
my $bin    = "$Msuite/bin";
my $cwd    = cwd();

## parameters
### TODO: add --no-rmdup support
our ($read1, $cycle, $index, $outdir, $read2, $readU) =
    ('',     0,      '',     ''     , ''    , ''    );
our ($mode3, $mode4, $protocol, $kit,       $thread, $phred33, $phred64, $minscore, $minsize) =
    (0,      0,      'BS',      'illumina', 0,       0,        0,        20,      , 20      );
our ($minins, $maxins, $minalign, $call_CpH, $alignonly, $no_rmdup) =
    (0,       1000,    0,         0,         0,          0        );
my $alignmode;
our $pe       = '';
our $help     = 0;
our $showVer  = 0;
our (@file1s, @file2s);
our $bowtie2_ver;

GetOptions(
	"1:s"  => \$read1,
	"x:s"  => \$index,
	"o:s"  => \$outdir,
	"c:i"  => \$cycle,
	"U:s"  => \$readU,

	"2:s"  => \$read2,
	"3"    => \$mode3,
	"4"    => \$mode4,
	"m:s"  => \$protocol,
	"k:s"  => \$kit,
	"p:i"  => \$thread,
	"q:i"  => \$minscore,
	"phred33"  => \$phred33,
	"phred64"  => \$phred64,
	"minsize:i"=> \$minsize,

	"minins:i" => \$minins,
	"maxins:i" => \$maxins,
	"Q:i"      => \$minalign,
	"CpH"      => \$call_CpH,

	"align-only"=> \$alignonly,
#	"no-rmdup" => \$no_rmdup,

	"help|h"    => \$help,
	"version|v" => \$showVer
);

if( $help ) {
	usage( );
	exit 0;
}

if( $showVer ) {
	print "$version\n";
	exit 0;
}

## check parameters
if( check_parameters() ) {
	usage( );
	exit 10;
}

## check whether the index is valid or not
unless( -s "$Msuite/index/$index/chr.info" ) {
	print STDERR "$Msuite/index/$index/chr.info\n";
	printRed( "Fatal error: The index you specified ($index) is invalid!\n" .
				"Please refer to README file for how to add indice to Msuite." );
	exit 11;
}

## check dependent programs
my ( $bowtie2, $samtools, $R ) = check_dependent_programs();

print "\nINFO: Use Msuite root directory '$Msuite'.\n";

#################################################################################
## prepare directories
$outdir = "$cwd/$outdir" unless $outdir =~ /^\//;
#$read1 = "$cwd/$read1"  unless $read1  =~ /^\//;
#$read2 = "$cwd/$read2"  unless $read2  =~ /^\//;

if( $phred33 ) {
	$minscore += 33;
} else {
	$minscore += 64;
}

# prepare makefile
my @tasks;
my $makefile = '';

# step 1: fastq trimming and alignment
my $Bowtie2Parameter = "-q --score-min L,0,-0.2 --ignore-quals --no-unal --no-head -p $thread --sam-no-qname-trunc";
my $PEdataParameter  = "--dovetail --minins $minins --maxins $maxins --no-mixed --no-discordant";
my $Mode4IndexCG2TG  = "$Msuite/index/$index/Mode4/CG2TG";
my $Mode4IndexCG2CA  = "$Msuite/index/$index/Mode4/CG2CA";
my $Mode3IndexC2T    = "$Msuite/index/$index/Mode3/C2T";
my $Mode3IndexG2A    = "$Msuite/index/$index/Mode3/G2A";
my $RawGenome        = "$Msuite/index/$index/genome.fa";
my $chrinfo          = "$Msuite/index/$index/chr.info";

my $thread_lim = $thread;
$thread_lim = 8 if $thread_lim > 8;	## limit the preprocessing programs to at most 8 threads due to I/O consideration

## deal with input files
if( $pe ) {	## paired-end data
	$read1 = join( ",", @file1s );
	$read2 = join( ",", @file2s );
	my $read1space = join( " ", @file1s );
	my $read2space = join( " ", @file2s );
	print "INFO: ", $#file1s+1, " paired files are specified as input in Paired-End mode.\n";
	$makefile .= "Msuite.trim.log: $read1space $read2space #-@ $thread_lim\n" .
					"\t$bin/preprocessor.pe $read1 $read2 $cycle Msuite $alignmode $thread_lim $minsize $minscore $kit\n\n";

	if( $alignmode == 4 ) {
		$makefile .= "Msuite.CG2TG.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --norc -x $Mode4IndexCG2TG $PEdataParameter " .
					 "-1 Msuite.R1.fq -2 Msuite.R2.fq -S Msuite.CG2TG.sam 2>Msuite.CG2TG.log\n".
					 "Msuite.CG2CA.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --nofw -x $Mode4IndexCG2CA $PEdataParameter " .
					 "-1 Msuite.R1.fq -2 Msuite.R2.fq -S Msuite.CG2CA.sam 2>Msuite.CG2CA.log\n".
					 "Msuite.merge.log: Msuite.CG2TG.sam Msuite.CG2CA.sam #-@ $thread_lim\n" .
					 "\t$bin/bowtie2.processer.pe Msuite.CG2TG.sam Msuite.CG2CA.sam Msuite.trim.log Msuite.merged.sam $thread_lim >Msuite.merge.log\n\n";
	} else {	## mode 3
		$makefile .= "Msuite.C2T.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --norc -x $Mode3IndexC2T $PEdataParameter " .
					 "-1 Msuite.R1.fq -2 Msuite.R2.fq -S Msuite.C2T.sam 2>Msuite.C2T.log\n" .
					 "Msuite.G2A.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --nofw -x $Mode3IndexG2A $PEdataParameter " .
					 "-1 Msuite.R1.fq -2 Msuite.R2.fq -S Msuite.G2A.sam 2>Msuite.G2A.log\n" .
					 "Msuite.merge.log: Msuite.C2T.sam Msuite.G2A.sam #-@ $thread_lim\n" .
					 "\t$bin/bowtie2.processer.pe Msuite.C2T.sam Msuite.G2A.sam Msuite.trim.log Msuite.merged.sam $thread_lim >Msuite.merge.log\n\n";
	}
} else {	# single-end data
	$read1 = join(",", @file1s);
	my $read1space = join( " ", @file1s );
	if( $#file1s == 0 ) {	## only 1 file
		print "INFO: 1 file is specified as input in Single-End mode.\n";
		$makefile .= "Msuite.trim.log: $read1 #-@ $thread_lim\n" .
		"\t$bin/preprocessor.se $read1 null $cycle Msuite $alignmode $thread_lim $minsize $minscore $kit\n\n";
	} else {	## there are multiple files
		print "INFO: ", $#file1s+1, " files are specified as input in Single-End mode.\n";
		$makefile .= "Msuite.trim.log: $read1space #-@ $thread_lim\n\t$bin/catfile " . join(" ", @file1s) . " | " .
		"$bin/preprocessor.se /dev/stdin null $cycle Msuite $alignmode $thread_lim $minsize $minscore\n\n";
	}

	if( $alignmode == 4 ) {
		$makefile .= "Msuite.CG2TG.sam: Msuite.trim.log\n" .
					 "\t$bowtie2 $Bowtie2Parameter --norc -x $Mode4IndexCG2TG " .
					 "-U Msuite.R1.fq -S Msuite.CG2TG.sam 2>Msuite.CG2TG.log\n" .
					 "Msuite.CG2CA.sam: Msuite.trim.log\n" .
					 "\t$bowtie2 $Bowtie2Parameter --nofw -x $Mode4IndexCG2CA " .
					 "-U Msuite.R1.fq -S Msuite.CG2CA.sam 2>Msuite.CG2CA.log\n" .
					 "Msuite.merge.log: Msuite.CG2TG.sam Msuite.CG2CA.sam\n" .
					 "\t$bin/bowtie2.processer.se Msuite.CG2TG.sam Msuite.CG2CA.sam Msuite.trim.log Msuite.merged.sam $thread_lim >Msuite.merge.log\n\n";
	} else {	## mode 3
		$makefile .= "Msuite.C2T.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --norc -x $Mode3IndexC2T " .
					 "-U Msuite.R1.fq -S Msuite.C2T.sam 2>Msuite.C2T.log\n" .
					 "Msuite.G2A.sam: Msuite.trim.log #-@ $thread\n" .
					 "\t$bowtie2 $Bowtie2Parameter --nofw -x $Mode3IndexG2A " .
					 "-U Msuite.R1.fq -S Msuite.G2A.sam 2>Msuite.G2A.log\n" .
					 "Msuite.merge.log: Msuite.C2T.sam Msuite.G2A.sam #-@ $thread_lim\n" .
					 "\t$bin/bowtie2.processer.se Msuite.C2T.sam Msuite.G2A.sam Msuite.trim.log Msuite.merged.sam $thread_lim >Msuite.merge.log\n\n";
	}
}
push @tasks, "Msuite.merge.log";

# step 2: remove duplicate
if( $pe ) {
	$makefile .= "Msuite.rmdup.log: Msuite.merge.log\n" .
				 "\t$bin/rmdup.pe $chrinfo Msuite.trim.log $maxins Msuite.merged.sam Msuite.rmdup\n" .
				 "Msuite.rmdup.size.dist.pdf: Msuite.rmdup.log\n" .
				 "\t$R --slave --args Msuite.rmdup.size.dist < $bin/plot.size.R\n";
	push @tasks, "Msuite.rmdup.size.dist.pdf";
} else {	## SE
	$makefile .= "Msuite.rmdup.log: Msuite.merge.log\n" .
				 "\t$bin/rmdup.se $chrinfo Msuite.trim.log Msuite.merged.sam Msuite.rmdup\n";
	push @tasks, "Msuite.rmdup.log";
}

# step 3: convert sam to bam and build bam index
$makefile .= "Msuite.rmdup.bam: Msuite.rmdup.log #-@ $thread\n".
			 "\tperl $bin/generate.header.pl $chrinfo $index $protocol $alignmode $read1 $read2 | cat - Msuite.rmdup.sam | $samtools view -bS -@ $thread -o Msuite.rmdup.bam -\n";
if( $pe ) {	## fixmate for PE data
	$makefile .= "Msuite.nsort.bam: Msuite.rmdup.bam #-@ $thread\n" .
				 "\t$samtools sort -n -@ $thread Msuite.rmdup.bam -o Msuite.nsort.bam\n" .
				 "Msuite.fixmate.bam: Msuite.nsort.bam #-@ $thread\n" .
				 "\t$samtools fixmate -@ $thread Msuite.nsort.bam Msuite.fixmate.bam\n" .
				 "Msuite.final.bam: Msuite.fixmate.bam #-@ $thread\n" .
				 "\t$samtools sort -@ $thread Msuite.fixmate.bam -o Msuite.final.bam\n".
				 "Msuite.final.bam.bai: Msuite.final.bam\n" .
				 "\t$samtools index Msuite.final.bam && rm -f Msuite.nsort.bam Msuite.fixmate.bam\n\n";
} else {
	$makefile .= "Msuite.final.bam: Msuite.rmdup.bam #-@ $thread\n" .
				 "\t$samtools sort -@ $thread Msuite.rmdup.bam -o Msuite.final.bam\n".
				 "Msuite.final.bam.bai: Msuite.final.bam\n" .
				 "\t$samtools index Msuite.final.bam\n\n";
}
push @tasks, "Msuite.final.bam.bai";

# step 4: fastq statistics and base composition plot
$makefile .= "R1.fqstat.pdf: Msuite.trim.log\n" .
			 "\t$R --slave --args R1.fqstat < $bin/plot.fqstatistics.R\n";
push @tasks, "R1.fqstat.pdf";
if( $pe ) {
$makefile .= "R2.fqstat.pdf: Msuite.trim.log\n" .
			 "\t$R --slave --args R2.fqstat < $bin/plot.fqstatistics.R\n";
push @tasks, "R2.fqstat.pdf";
}
$makefile .= "\n";

unless( $alignonly ) {
	# step 5: methyaltion call
	if( $pe ) {
		$makefile .= "Msuite.CpG.meth.call: Msuite.rmdup.sam\n".
					 "\t$bin/meth.caller.CpG PE $RawGenome Msuite.rmdup.sam $protocol $cycle $minalign Msuite\n";

		$makefile .= "Msuite.CpH.meth.call: Msuite.rmdup.sam\n".
					 "\t$bin/meth.caller.CpH PE $RawGenome Msuite.rmdup.sam $protocol $cycle $minalign Msuite\n";

		$makefile .= "Msuite.R1.mbias.pdf: Msuite.CpG.meth.call\n" .
					 "\t$R --slave --args Msuite.R1.mbias $protocol < $bin/plot.Mbias.R\n";
		$makefile .= "Msuite.R2.mbias.pdf: Msuite.CpG.meth.call\n" .
					 "\t$R --slave --args Msuite.R2.mbias $protocol < $bin/plot.Mbias.R\n\n";
		push @tasks, "Msuite.R1.mbias.pdf Msuite.R2.mbias.pdf";
	} else  {
		$makefile .= "Msuite.CpG.meth.call: Msuite.rmdup.sam\n".
					 "\t$bin/meth.caller.CpG SE $RawGenome Msuite.rmdup.sam $protocol $cycle $minalign Msuite\n";

		$makefile .= "Msuite.CpH.meth.call: Msuite.rmdup.sam\n".
					 "\t$bin/meth.caller.CpH SE $RawGenome Msuite.rmdup.sam $protocol $cycle $minalign Msuite\n";

		$makefile .= "Msuite.R1.mbias.pdf: Msuite.CpG.meth.call\n" .
					 "\t$R --slave --args Msuite.R1.mbias $protocol < $bin/plot.Mbias.R\n\n";
		push @tasks, "Msuite.R1.mbias.pdf";
	}
	$makefile .= "DNAm.per.chr.pdf: Msuite.CpG.meth.call\n" .
				 "\t$R --slave --args Msuite.CpG.meth.log $protocol DNAm.per.chr < $bin/plot.DNAm.per.chr.R\n\n";

	push @tasks, "DNAm.per.chr.pdf";
	push @tasks, "Msuite.CpH.meth.call" if $call_CpH;

	## step 6: plot DNAm around TSS
	my $TSS = "$Msuite/index/$index/tss.ext.bed";
	$makefile .= "DNAm.around.TSS.stat: Msuite.CpG.meth.call\n" .
#				 "\tperl $bin/profile.DNAm.around.TSS.pl Msuite.meth.call $TSS $protocol > DNAm.around.TSS.stat\n" .
				 "\t$bin/profile.DNAm.around.TSS $chrinfo Msuite.CpG.meth.call $TSS $protocol > DNAm.around.TSS.stat\n" .
				 "DNAm.around.TSS.pdf: DNAm.around.TSS.stat\n" .
				 "\t$R --slave --args DNAm.around.TSS.stat DNAm.around.TSS < $bin/plot.DNAm.around.tss.R\n\n";
	push @tasks, "DNAm.around.TSS.pdf";
} else {
	printRed( "Warning: Align-only Mode is ON!" );
}

## step 7: generate final report
my $report = join(" ", "Msuite.report/index.html:", @tasks);
$report .= "\n\tmv $outdir/*png $outdir/*pdf $outdir/Msuite.report; perl $Msuite/bin/make.report.pl $outdir $alignonly\n\n";

# write makefile
prepare_directories();
open  MK, ">$outdir/makefile" or die("$!");
print MK  $report, $makefile;
print MK  "clean:\n\t\@if [ -s \"Msuite.report/index.html\" ];then rm -f *fq *A.sam *T.sam *merged.sam Msuite.rmdup.bam;else echo \"Error: it seems that the analysis has not finished yet.\";fi\n\n";
close MK;

print "\nMakefile successfully generated.\n",
	  "Now you can go to '$outdir' and run 'make' to perform the analysis.\n\n";

#################################### subroutines ###########################################
# prepare directories
sub prepare_directories {
	if( -d $outdir ) {
		printYlw( "WARNING: output directory '$outdir' EXISTS!" );
	} elsif( -s $outdir ) {
		printRed( "FATAL ERROR: output directory '$outdir' EXISTS and it is a FILE!" );
		exit 100;
	} else {
		system "mkdir -p $outdir/Msuite.report/";
	}

	## write configuration file
	open OUT, ">$outdir/Msuite.conf" or die("$!");
	if( $pe ) {
		print OUT "Sequencing files\t$read1:$read2\n",
				"Sequencing mode\tPaired-end\n";
	} else {
		print OUT "Sequencing files\t$read1\n",
				"Sequencing mode\tSingle-end\n";
	}
	print OUT "Sequencing cycle\t$cycle\n",
			  "Library protocol\t$protocol\n",
			  "Genome index\t$index\n",
			  "Alignment mode\t$alignmode-letter\n";

	if( $phred33 ) {
		print OUT "Quality score base\tPhred33\n";
	} else {
		print OUT "Quality score base\tPhred64\n";
	}
	print OUT "Minimum quality score\t$minscore\n",
			  "Bowtie2 version\t$bowtie2_ver\n",
			  "Insert size range\t$minins-$maxins\n",
			  "Minimum alignment score to call methylation\t$minalign\n",
			  "Methylation call for CpH sites\t", ($call_CpH)?'Yes':'No', "\n",
			  "Align-only mode\t", ($alignonly)?'On':'Off', "\n",
			  "Running thread\t$thread\n",
			  "Output directory\t$outdir\n";
	close OUT;
}

sub usage {
	print <<END_OF_USAGE;

########## Msuite: Multi-mode DNA methylation data analysis suite ##########

Author : Kun Sun (sunkun\@szbl.ac.cn)
Version: $version

\033[1;34mUsage: msuite [options] -x index -1/-U Read1.fq [ -2 Read2.fq ] -o out.dir\033[0m

Compulsory parameters:

  -1/-U Read1.fq   Specify the path to the files containing read 1
                   If your data is Paired-end, specify read 2 files using '-2' option
                   Note that if -U is used, '-2' will be ignored no matter it's set or not

                   If you have multiple files for your sample, please use ',' to separate them,
                   or use '*' syntax similar to BASH (within a pair of single quotation marks)
				   Currently, the maximum number of reads that Msuite could handle is 4,294,967,295.

  -x index         Specify the genome index
                   Please refer to README file on how to build index for Msuite

  -o out.dir       Specify the output directory
                   Note that your specified directory will be created if it does not exist
                   otherwise the files under that directory could get over-written


Optional parameters:

  -2 Read2.fq      Specify the path to the file containing read 2
                   Use this parameter if your data is generated in paired-end mode

                   If you have multiple files for your sample, please use ',' to separate them,
                   or use '*' syntax; make sure that all the files are well paired in '-1' and '-2'

  -3               Use 3-letter alignment (default)
  -4               Use 4-letter alignment
                   Note that the above two options are mutually exclusive

  -m TAPS/BS       Specify the library protocol (default: BS)
                   Note that only 'TAPS' and 'BS' are acceptable

  -c cycle         Specify the seqeuencing cycles of the data (default: auto-detect)
                   Default: auto-detected

  -k kit           Specify the library preparation kit (default: illumina)
                   Note that the current version supports 'illumina', 'nextera' and 'bgi'

  --phred33        Read cycle quality scores are in Phred33 format (default)
  --phred64        Read cycle quality scores are in Phred64 format
                   Note that the above two options are mutually exclusive

  -q score         The minimum quality score to keep the cycle (default: 20)
                   Note that 20 means 1% error rate, 30 means 0.1% error rate in Phred

                   Sometimes quality scores start from 35 ('#') in the FASTQ files,
                   in this case you could adjust '-q' option, e.g., '--phred33 -q 22'

  --minsize size   Minimum read size to be kept for alignment (default: 20)

  --minins MIN     Minimum insert size (default: 0)
  --maxins MAX     Maximum insert size (default: 1000)
                   Note that the above two options will be ignored for Single-End data

  --align-only     Stop after alignment (i.e., do not perform DNA methylation call and
                   visualization around TSS; default: not set)

  -Q score         The minimum alignment score for a read to call methylation (default: 0)
  --CpH            Set this flag to call methylation status of CpH sites (default: not set)

  -p threads       Specify how many threads should be used (default: use all threads)

  -h/--help        Show this help information and quit
  -v/--version     Show the software version and quit


Please refer to README file for more information.

END_OF_USAGE
}

sub check_parameters {
	print "\n";

	if( $readU ) {
		if( $read1 ) {
			printRed( "Error: Both -1 and -U are specified!" );
			return 1;
		}
		$read1 = $readU;
		if( $read2 ) {
			printRed( "Warning: -U is specified then -2 will be IGNORED!" );
			$read2 = '';
		}
	}

	unless( $read1 && $index && $outdir ) {
		printRed( "Fatal error: Missing key parameters!" );
		#print "R1: $read1\nIndex: $index\nOutDir: $outdir\nCycle: $cycle\n";
		return 1;
	}

	## process input files
	$read1 =~ s/,+$//;
	$read1 =~ s/^,+//;
	## check whether '*' syntax is used in file names
	if( index($read1, '*') >= 0 ) {
		my $filelist = `ls $read1`;
		@file1s = split /\s+/, $filelist;
		$read1  = join(",", @file1s);
		printGrn("IMPORTANT INFORMATION: YOU USED '*' SYNTAX IN READ1, WHICH IS EXTRACTED AS:");
		printGrn( $read1 );
	} else {
		@file1s = split /,+/, $read1;
	}
	if( $#file1s < 0 ) {
		printRed( "Fatal error: Read1 does not contain any valid files!" );
		exit 10;
	}

	if( $read2 ) {	# paired-end data
		$pe = 'PE';
		print "INFO: The input reads will be processed in Paired-End mode.\n";
		$read2 =~ s/,+$//;
		$read2 =~ s/^,+//;
		if( index($read2, '*') >= 0 ) {
			my $filelist = `ls $read2`;
			@file2s = split /\s+/, $filelist;
			$read2  = join(",", @file2s);
			printGrn("IMPORTANT INFORMATION: YOU USED '*' SYNTAX IN READ2, WHICH IS EXTRACTED AS:");
			printGrn( $read2 );
		} else {
			@file2s = split /,+/, $read2;
		}

		if( $#file2s < 0 ) {
			printRed( "Fatal error: Read2 does not contain any valid files!" );
			exit 10;
		}
		if( $#file1s != $#file2s ) {
			printRed( "Fatal error: Read1 and Read2 do not contain the same amount of files!\n" );
			exit 11;
		}
	} else {
		$pe = '';
		print "INFO: The input reads will be processed in Single-End mode.\n";
	}

	## change to absolute path
	for( my $i=0; $i<=$#file1s; ++$i ) {
		$file1s[$i] = "$cwd/$file1s[$i]" unless $file1s[$i] =~ /^\//;
		if( $pe ) {
			$file2s[$i] = "$cwd/$file2s[$i]" unless $file2s[$i] =~ /^\//;
		}
	}

	if( $cycle == 0 ) {	## detect the sequencing cycle
		print "INFO: auto-detect read cycles using $file1s[0] file.\n";
		open FQ, "less $file1s[0] |" or die("$!");	## in case that it is a gz file
		my $MAX = 10000;	##read the first 1000 line, and use the largest read size
		my $num = 0;
		while( <FQ> ) {
			++ $num;
			last if $num >= $MAX;

			my $seq = <FQ>;
			<FQ>;
			<FQ>;
			chomp( $seq );
			$cycle = length($seq) if length($seq) > $cycle;
		}
		close FQ;

		print "INFO: Sequencing cycle is determined to be $cycle.\n";
	} else {
		print "INFO: Sequencing cycle is set, only the first $cycle cycles will be analyzed.\n";
	}

	if( $mode3 && $mode4 ) {
		printRed( "Error: Both -3 and -4 are specified!" );
		return 1;
	} else {
		if( $mode4 ) {
			$alignmode = 4;
		} else {	## default
			$alignmode = 3;
		}
	}

	if( $phred33 && $phred64 ) {
		printRed( "Error: Both --phred33 and --phred64 are specified!" );
		return 1;
	} else {
		if( $phred64 ) {
			$phred64 = 1;
			$phred33 = 0;
		} else {	## default
			$phred64 = 0;
			$phred33 = 1;
		}
	}

	$protocol = uc $protocol;
	if( $protocol !~ /^TAPS$/i && $protocol !~ /^BS$/i ) {
		printRed( "Error: Unacceptable library protocol parameter!" );
		return 1;
	} else {
		if ( $protocol =~ /^BS$/i ) {	## for BS-seq, force to use 3-letter mode
			print "INFO: The data is generated in BS-seq protocol.\n";
			if( $alignmode == 4 ) {
				$alignmode = 3;
				print "INFO: For BS-seq data, only 3-letter mode is valid ('-4' option will be ignored).\n";
			}
		} else {
			print "INFO: The data is generated in TAPS-like protocol.\n";
		}
	}

	if( $kit =~ /^illumina$/i ) {
		$kit = "illumina";
	} elsif ( $kit =~ /^nextera$/i ) {
		$kit = "nextera";
	} elsif( $kit =~ /^bgi$/i ) {
		$kit = "bgi";
	} else {
		printRed( "Error: Unacceptable library preparation kit parameter!" );
		return 1;
	}

	if( $thread == 0 ) {
		$thread = `grep processor /proc/cpuinfo | wc -l`;
		chomp( $thread );
	}

	if( $minins>$maxins || $maxins==0 ) {
		printRed( "Error: Unacceptable insert size range!" );
		return 1;
	}

	return 0;
}

sub check_dependent_programs {
	my $bowtie2 = `which bowtie2 2>/dev/null`;
	chomp( $bowtie2 );
	if( $bowtie2 ) {
		my $info = `$bowtie2 --version`;
		my @l = split /\n/, $info;
		foreach ( @l ) {
			if( /version (\S+)/ ) {
				$bowtie2_ver = $1;
				last;
			}
		}
		$bowtie2_ver = "unknown" unless $bowtie2_ver;
		print "INFO: bowtie2 (version $bowtie2_ver) found at '$bowtie2'.\n";
	} else {
		printRed( "Fatal error: could not locate 'bowtie2' in your path!" );
	exit 20;
	}

	my $samtools = `which samtools 2>/dev/null`;
	chomp( $samtools );
	if( $samtools ) {
		my $info = `$samtools 2>&1`;
		my $ver;
		my @l = split /\n/, $info;
		foreach ( @l ) {
			$ver = $1 if /^Version:\s(\S+)/;
		}
		$ver = "unknown" unless $ver;
		print "INFO: samtools (version $ver) found at '$samtools'.\n";
	} else {
		printRed( "Fatal error: could not locate 'samtools' in your path!" );
		exit 20;
	}

	my $R = `which R`;
	chomp( $R );
	if( $R ) {
		my $info = `$R --version`;
		my $ver;
		my @l = split /\n/, $info;
		foreach ( @l ) {
			$ver = $1 if /version (\S+)/;
		}
		$ver = "unknown" unless $ver;
		print "INFO: R (version $ver) found at '$R'.\n";

	} else {
		printRed( "Fatal error: could not locate 'R' in your path!" );
		exit 20;
	}

	return ($bowtie2, $samtools, $R);
}

sub printRed {
	my $info = shift;
	print STDERR "\n\033[1;31m$info\033[0m\n\n";
}

sub printGrn {
	my $info = shift;
	print STDERR "\033[1;32m$info\033[0m\n";
}

sub printYlw {
	my $info = shift;
	print STDERR "\n\033[1;33m$info\033[0m\n\n";
}

